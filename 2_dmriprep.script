#!/bin/tcsh

set nonomatch

set DATADIR = /proj/NIRAL/studies/IBIS/EP/EP_Data
set PROCDIR = /proj/NIRAL/studies/IBIS/EP/processing/dMRI

set dmriprepVersion = dmriprep_0.5.7

#foreach APfile ($DATADIR/??????/V*/mri/native/dMRI/188700_V12*dir101_AP_select.nrrd)
#foreach APfile ($DATADIR/??????/V*/mri/native/dMRI/*dir101_AP_select_secondary.nrrd)
foreach APfile ($DATADIR/??????/V*/mri/native/dMRI/*dir101_AP_select.nrrd)

    set folder = $APfile:h

    set APfileList = $folder/*dir101_AP_select.nrrd
    set PAfileList = $folder/*dir101_PA_select.nrrd
    if ($#APfileList > 1 || $#PAfileList > 1) then
	echo $folder has more than each one AP/PA file: $#APfileList / $#PAfileList
	continue
    endif
    
    set PAfile = $PAfileList[1]
    if (! -e $PAfile) then

	set PAB0fileList = $folder/DTIPrep1.2.11c_B0_QC/*dir101_B0_QCed_VC.nrrd
	if (-e $PAB0fileList[1]) then
	    echo FYI no selected PAfile in $folder, found B0 PA, not doing anything with it yet
	else
	    echo FYI no selected PAfile in $folder, and no B0 PA
	endif
	continue
    endif

    set session = $folder:h:h:h:t
    set subject = $folder:h:h:h:h:t

    #echo doing $subject $session

    set outputFolder = $folder/$dmriprepVersion
    set prefix = ${subject}_${session}_101dir

    if (! -e $outputFolder) mkdir $outputFolder

    set outputFile = $outputFolder/${prefix}_QCed.nrrd
    if (-e $outputFile && ( ( -M $APfileList ) > ( -M $outputFile))) then
	echo selected file is newer than result, recomputing $APfileList
    endif
   
    #if (! -e $outputFile) then
    if (! -e $outputFolder/log.txt) then
	echo running DWI QC on $folder - $prefix
	$PROCDIR/Run_dmriprep_wrapper.script $APfile $PAfile $outputFolder $prefix 
	#exit
    endif

    #add tractography, as it was not part of the original 
    #set outputTracts = $outputFolder/tracts/${prefix}_QCed_tractogram.vtk
    #if (! -e $outputTracts) then
	#echo doing tracography $outputTracts
	#$PROCDIR/Run_dmriprep_tract_wrapper.script $outputFile $outputFolder/tracts $prefix 
    #endif

end
